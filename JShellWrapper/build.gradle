import java.time.Instant

plugins {
    id 'application'
    id 'com.google.cloud.tools.jib' version '3.4.2'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

/* Application plugin is used for debugging */
application {
    mainClassName = 'org.togetherjava.jshell.Main'
    run {
        standardInput = System.in
        systemProperty 'line.separator', '\n'
        systemProperty 'file.encoding', 'UTF-8'
    }
}

test {
    systemProperty 'line.separator', '\n'
    systemProperty 'file.encoding', 'UTF-8'
}

jib {
    from.image = 'eclipse-temurin:22-alpine'
    to {
        image = rootProject.ext.jShellWrapperImageName
        auth {
            username = System.getenv('ORG_REGISTRY_USER') ?: ''
            password = System.getenv('ORG_REGISTRY_PASSWORD') ?: ''
        }
    }
    container {
        ports = [ "8081-8081" ]
        mainClass = 'org.togetherjava.jshell.Main'
        setCreationTime(Instant.now().toString())
    }
}

shadowJar {
    archiveBaseName.set('JShellWrapper')
    archiveClassifier.set('')
    archiveVersion.set('')
}
