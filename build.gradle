buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.25.0'
    }
}

group = 'org.togetherjava'
version = project.version

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            targetExclude("build/**")
            endWithNewline()
            removeUnusedImports()
            // empty string '' covers all imports that aren't explicitly specified,
            // we use it as catch-all for external dependencies like JDA
            // '\\#` is prefix for static imports
            importOrder('','org.togetherjava', 'javax', 'java', '\\#')
            // TODO: pinning version because of spotless error https://github.com/diffplug/spotless/issues/1992
            eclipse("4.31").configFile("${rootProject.rootDir}/spotless.xml")
        }
    }

    group = 'org.togetherjava'
    version = project.version

    tasks.withType(JavaCompile).configureEach {
        options.release = 21
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(21))
        }
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        maxParallelForks = Runtime.runtime.availableProcessors()
        systemProperty 'line.separator', '\n'
        systemProperty 'file.encoding', 'utf-8'
    }

    sourceSets {
        test {
            java {
                srcDirs 'src/test/integration', 'src/test/unit'
            }
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
    }
}
